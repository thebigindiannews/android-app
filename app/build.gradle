apply plugin: 'com.android.application'
apply plugin: 'android-apt'

def getApiKey(){
    def Properties props = new Properties()
    props.load(new FileInputStream(new File('secrets.properties')))
    return props['API_KEY']
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.enamakel.thebigindiannews"
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 44
        versionName "2.5"
        buildConfigField "String", "GITHUB_TOKEN", "\"\""
        buildConfigField "String", "READABILITY_TOKEN", "\"\""
    }

    buildTypes {
        leak.initWith(buildTypes.debug)
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "API_KEY", "\""+getApiKey()+"\""
        }
    }

    lintOptions {
        htmlReport false
        xmlReport false
        textReport true
        warningsAsErrors true
        abortOnError true
        informational 'Overdraw' // http://b.android.com/169879
        explainIssues false
        absolutePaths false
    }
}

ext {
    supportVersion = '23.1.1'
    okHttpVersion = '3.0.0-RC1'
    daggerVersion = '1.2.2'
    leakCanaryVersion = '1.3.1'
    retrofit2Version = '2.0.0-beta3'
    retrofitVersion = '2.0.0-beta2'
    AAVersion = "3.3.2"
    lombokVersion = "1.16.6"
    autoParcelVersion = "0.1"
    picassoVersion = "2.5.2"
}

apt {
    arguments {
        androidManifestFile variant.outputs[0]?.processResources?.manifestFile
        // if you have multiple outputs (when using splits), you may want to have other index than 0

        // you should set your package name here if you are using different application IDs
        // resourcePackageName "your.package.name"

        // You can set optional annotation processing options here, like these commented options:
        // logLevel 'INFO'
        // logFile '/var/log/aa.log'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:preference-v7:$supportVersion"
    compile "com.android.support:customtabs:$supportVersion"
    compile "com.squareup.dagger:dagger:$daggerVersion"
    leakCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    debugCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    compile "com.squareup.retrofit:retrofit:$retrofitVersion"
    compile ("com.squareup.retrofit2:retrofit:$retrofit2Version") {
        // exclude Retrofitâ€™s OkHttp peer-dependency module and define your own module import
        exclude module: 'okhttp'
    }
    compile "com.squareup.retrofit2:converter-gson:$retrofit2Version"
    compile "com.squareup.okhttp3:okhttp:$okHttpVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    provided "com.squareup.dagger:dagger-compiler:$daggerVersion"
    apt "org.androidannotations:androidannotations:$AAVersion"
    compile "org.androidannotations:androidannotations-api:$AAVersion"
    compile "org.projectlombok:lombok:$lombokVersion"
    compile "me.tatarka:auto-parcel-gson:$autoParcelVersion"
    apt "me.tatarka:auto-parcel-gson-processor:$autoParcelVersion"
    compile "com.squareup.picasso:picasso:$picassoVersion"
}

apply from: "${rootProject.getRootDir()}/robolectric.gradle"
apply from: "${rootProject.getRootDir()}/jacoco.gradle"
